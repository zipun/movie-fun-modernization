import org.flywaydb.gradle.FlywayExtension
import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript {
    dependencies {
        classpath "mysql:mysql-connector-java:$mysqlVersion"
    }
}

plugins {
    id "org.flywaydb.flyway" version "5.2.1"
}

apply from: "../db.gradle"

flyway {
    url = "jdbc:mysql://localhost:3306/albums?useSSL=false"
    user = "root"
    outOfOrder = false
}

def tunnelProcess

task openTunnel {
    doLast {
        println 'Opening Tunnel'
        Thread.start {
            tunnelProcess = "cf ssh -N -L 63306:${getMysqlHost("album-service", "album-database")}:3306 album-service".execute()
        }

        sleep 5_000L

        if (tunnelProcess == null) throw new Exception("Unable to open tunnel!")
    }
}

task closeTunnel {
    doLast {
        println 'Closing Tunnel'
        tunnelProcess.destroy()
    }
}

task cfMigrate(type: FlywayMigrateTask, dependsOn: openTunnel) {
    if (System.env.CF_MIGRATE) {
        extension = setupProdFlywayExtension(new FlywayExtension(), "album-service", "album-database")
    }
}

cfMigrate.finalizedBy closeTunnel
